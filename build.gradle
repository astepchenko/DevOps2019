plugins {
  //id 'java'
  id 'war'
}

repositories {
  mavenLocal()
  mavenCentral()
  jcenter()
}

dependencies {
  // https://mvnrepository.com/artifact/log4j/log4j
  compile group: 'log4j', name: 'log4j', version: '1.2.17'
  // https://mvnrepository.com/artifact/commons-io/commons-io
  compile group: 'commons-io', name: 'commons-io', version: '2.6'
}

war {
  dependsOn 'release'
  archiveName = 'greeter.war'
}

task release {
  doLast {
    println '=== Building release version ' + version
    println '=== Run \'./gradlew increment\' to update version'
    def file = new File ('./build/resources/main/greeting.txt')
    file.write (version)
  }
}

task increment {
  doLast {
    println '=== Incrementing version'
    println 'Version before ' + version
    def parser = /(?<major>\d+).(?<minor>\d+).(?<patch>\d+)/
    def match = version =~ parser
    if ( match.matches () ) {
      def (major, minor, patch) = ['major', 'minor', 'patch'].collect { match.group (it) }
      patch = (patch as Integer) + 1
      def newversion = major + '.' + minor + '.' + patch
      println 'Version after ' + newversion
      println 'Adding new version to gradle.properties'
      def file = new File ('gradle.properties')
      file.write ('version=' + newversion)
    }
  }
}
